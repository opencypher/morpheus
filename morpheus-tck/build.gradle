
description = 'Cypher for Apache Spark - TCK'

sourceSets{
    generator{}
}

dependencies {
    testImplementation project(':okapi-tck')
    testImplementation project(':morpheus-testing')
    testImplementation libs.scala.spark.sql
    generatorImplementation project(':okapi-tck')
    generatorImplementation project(':morpheus-testing')
}

// split scenario name / key-words with | --> debugTCKScenarios -Pscenarios = 'sc1|sc2|..'
tasks.register('debugTCKScenarios', JavaExec) {
    description = 'Generates scala-test from TCK scenarios (keywords of the scenario names suffice)'
    group = 'generator'
    dependsOn sourceSets.generator.runtimeClasspath

    def outPath = project.findProperty('outDir') ?: 'src/test/scala/org/opencypher/morpheus/testing/'
    def resDir = project.findProperty('resDir') ?: 'src/test/resources/'
    def scenarios = project.findProperty('scenarios') ?: ''

    mainClass = 'org.opencypher.morpheus.testing.MorpheusTestGenerator'
    classpath =  sourceSets.generator.runtimeClasspath
    args = [outPath, resDir, scenarios]
}
