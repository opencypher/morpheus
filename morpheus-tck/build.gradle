
description = 'Cypher for Apache Spark - TCK'

sourceSets{
    generator{}
}

dependencies {
    testImplementation project(':okapi-tck')
    testImplementation project(':morpheus-testing')
    testImplementation libs.spark.sql
    generatorImplementation project(':okapi-tck')
    generatorImplementation project(':morpheus-testing')
}

// split scenario name / key-words with | --> debugTCKScenarios -Pscenarios = 'sc1|sc2|..'
task debugTCKScenarios() {
    description = 'Generates scala-test from TCK scenarios (keywords of the scenario names suffice)'
    group = 'generator' 
    def outPath = project.findProperty('outDir') ?: 'src/test/scala/org/opencypher/morpheus/testing/'
    def resDir = project.findProperty('resDir') ?: 'src/test/resources/'
    def scenarios = project.findProperty('scenarios') ?: ''
    dependsOn sourceSets.generator.runtimeClasspath
    doLast {
        javaexec {
            classpath =  sourceSets.generator.runtimeClasspath
            main = 'org.opencypher.morpheus.testing.MorpheusTestGenerator'
            args = [outPath,resDir,scenarios]
        }
    }
}
