group = "${group}.documentation"


tasks.findAll { it instanceof Jar }.each { it.enabled = false }

task asciidocJar(type: Jar) {
    group 'documentation'
    description 'Package asciidoc source files.'
    classifier = 'asciidoc'
    from("asciidoc")
}

task aggregatedScalaDoc(type: ScalaDoc) {
    ext.fromProjects = [
            project(':okapi-api'),
            project(':spark-cypher'),
            project(':spark-cypher-examples'),
    ]

    fromProjects.each {
        source it.sourceSets.main.allSource
    }

    include '**/api/**/*.scala'
    include '**/api/**/*.java'
    include '**/examples/**/*.scala'
    include '**/examples/**/*.java'

    scalaDocOptions.additionalParameters = scalacParameters
}

task aggregatedScalaDocJar(type: Jar) {
    classifier = 'javadoc'
    from tasks.aggregatedScalaDoc
}

pub.full.artifacts = [
        'aggregatedScalaDocJar',
        'asciidocJar',
]
pub.dev.artifacts = [
        'aggregatedScalaDocJar',
        'asciidocJar',
]

dependencies {
    tasks.aggregatedScalaDoc.fromProjects.each {
        compileOnly it
    }
}
