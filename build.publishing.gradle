subprojects {
    apply plugin: 'maven-publish'

    ext {
        pub = [dev : [artifacts: ['jar',
                                  'sourceJar',]],
               full: [artifacts: ['jar',
                                  'sourceJar',
                                  'docJar',
                                  'testJar',]],
        ]
    }

    publishing {

        repositories {
            maven {
                name = 'buildDir'
                url = "file://${rootProject.file(cfg.publishDir)}"
            }
        }

        publications {

            dev(MavenPublication) {
                from components.java

                afterEvaluate {
                    artifacts = pub.dev.artifacts
                            .findResults { tasks.findByName(it) }
                            .findAll { it.enabled }
                }

                pom {
                    licenses {
                        license {
                            name = 'Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0'
                        }
                    }
                    developers {
                        developer {
                            id = 'caps'
                            name = 'The CAPS team'
                            email = 'opencypher@neo4j.com'
                            url = 'https://www.opencypher.org'
                        }
                    }
                    scm {
                        url = 'https://github.com/opencypher/cypher-for-apache-spark'
                    }
                }
            }

            full(MavenPublication) {
                from components.java

                afterEvaluate {
                    artifacts = pub.full.artifacts
                            .findResults { tasks.findByName(it) }
                            .findAll { it.enabled }

                }

                pom {
                    licenses {
                        license {
                            name = 'Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0'
                        }
                    }
                    developers {
                        developer {
                            id = 'caps'
                            name = 'The CAPS team'
                            email = 'opencypher@neo4j.com'
                            url = 'https://www.opencypher.org'
                        }
                    }
                    scm {
                        url = 'https://github.com/opencypher/cypher-for-apache-spark'
                    }
                }
            }
        }
    }

    tasks.withType(PublishToMavenRepository) {
        onlyIf { publication == publishing.publications.full }
    }

    // Convenience for quick publish to maven local
    task devPublish {
        group 'publishing'
        description ' Publishes main jars to the local Maven repository.'
        dependsOn tasks.publishDevPublicationToMavenLocal
    }

    // Task run by teamcity
    task ci {
        dependsOn tasks.check
        dependsOn tasks.publish
    }

}
