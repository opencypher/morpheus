apply plugin: 'com.github.johnrengelman.shadow'

description = 'Morpheus: Cypher for Apache Spark'

if (project.hasProperty('testOkapiShade')) {
    configurations {
        compile {
            exclude(group: 'org.opencypher', module: 'okapi-api')
            exclude(group: 'org.opencypher', module: 'okapi-ir')
            exclude(group: 'org.opencypher', module: 'okapi-logical')
            exclude(group: 'org.opencypher', module: 'okapi-relational')
            exclude(group: 'org.opencypher', module: 'okapi-trees')
        }
    }
    dependencies {
        implementation group: 'org.opencypher', name: 'okapi-shade', version: ver.self
    }
}

dependencies {
    api project(':okapi-relational')
    api project(':okapi-neo4j-io')
    api project(':graph-ddl')

    implementation(libs.scala.upickle.get())
        .exclude(group: libs.exclusions.utest.get().group, module: libs.exclusions.utest.get().name)
        .exclude(group: libs.exclusions.acyclic.get().group, module: libs.exclusions.acyclic.get().name)

    implementation libs.scala.typelevel.cats.core
    implementation libs.neo4j.java.driver

    compileOnly libs.scala.spark.core
    compileOnly libs.scala.spark.sql
    compileOnly libs.scala.spark.catalyst
}

shadowJar {
    archiveClassifier = 'all'
    from project.sourceSets.main.output

    dependencies {
        exclude(dependency('org.scala-lang:'))
        exclude(dependency('org.scala-lang.modules:'))
    }
    exclude "META-INF/versions/**/*"
}

pub.full.artifacts += 'shadowJar'
