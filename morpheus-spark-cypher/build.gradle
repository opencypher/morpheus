apply plugin: 'com.gradleup.shadow'

description = 'Morpheus: Cypher for Apache Spark'

if (project.hasProperty('testOkapiShade')) {
    configurations {
        compile {
            exclude(group: 'org.opencypher', module: 'okapi-api')
            exclude(group: 'org.opencypher', module: 'okapi-ir')
            exclude(group: 'org.opencypher', module: 'okapi-logical')
            exclude(group: 'org.opencypher', module: 'okapi-relational')
            exclude(group: 'org.opencypher', module: 'okapi-trees')
        }
    }
    dependencies {
        implementation group: 'org.opencypher', name: 'okapi-shade', version: ver.self
    }
}

configurations.implementation {
    exclude(
        group: libs.exclusions.lihaoyi.acyclic.get().group,
        module: libs.exclusions.lihaoyi.acyclic.get().name
    )
    exclude(
        group: libs.exclusions.lihaoyi.utest.get().group,
        module: libs.exclusions.lihaoyi.utest.get().name
    )
}

dependencies {
    api project(':okapi-relational')
    api project(':okapi-neo4j-io')
    api project(':graph-ddl')

    implementation libs.upickle
    implementation libs.typelevel.cats.core
    implementation libs.neo4j.java.driver

    compileOnly libs.spark.catalyst
    compileOnly libs.spark.core
    compileOnly libs.spark.sql
}

shadowJar {
    archiveClassifier = 'all'
    from project.sourceSets.main.output

    dependencies {
        exclude(dependency('org.scala-lang:'))
        exclude(dependency('org.scala-lang.modules:'))
    }
    exclude "META-INF/versions/**/*"
}

pub.full.artifacts += 'shadowJar'
